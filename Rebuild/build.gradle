apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply from: "${rootDir}/gradle/gradle-findbugs/findbugs.gradle"


repositories {
    maven {
        url 'https://raw.github.com/rejasupotaro/AsyncRssClient/master/AsyncRssClient/repository'
    }
    maven { url 'https://raw.github.com/rejasupotaro/RobotGirl/master/RobotGirl/repository' }
    flatDir {
        dirs 'src/main/libs'
    }
    google()
    mavenCentral()
}

def versionMajor = 1
def versionMinor = 0
def versionPatch = 0
def versionBuild = 0 // bump for dogfood builds, public betas, etc.
def revisionHash = ["sh", "-c", "cd ${project.rootDir} ; git rev-parse --short HEAD"].execute().in.text.trim()

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.2"

    useLibrary 'org.apache.http.legacy'

    defaultConfig {
        applicationId "rejasupotaro.rebuild"
        minSdkVersion 21
        targetSdkVersion 25
        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}-${revisionHash}"
    }

    def hasReleaseAuthority = new File("${rootDir}/secret.gradle").exists()
    if (hasReleaseAuthority) {
        apply from: "${rootDir}/secret.gradle"
        signingConfigs {
            release {
                storeFile file('rebuild.keystore')
                storePassword project.storePassword
                keyAlias project.keyAlias
                keyPassword project.keyPassword
            }
        }
    }

    buildTypes {
        debug {
            debuggable true
            versionNameSuffix '.debug'
        }

        if (hasReleaseAuthority) {
            release {
                signingConfig signingConfigs.release
                debuggable false
                proguardFile getDefaultProguardFile('proguard-android-optimize.txt')
                proguardFile file('proguard-release.txt')
            }
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'ic_launcher.png'
    }

    lintOptions {
        abortOnError false
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(dir: 'src/main/libs', include: '*.jar')
    implementation "com.android.support:appcompat-v7:${support_library_version}"
    implementation "com.android.support:recyclerview-v7:${support_library_version}"
    implementation "com.android.support:cardview-v7:${support_library_version}"

    implementation 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'
    implementation 'rejasupotaro:async-rss-client:0.0.6'
    implementation 'com.squareup:otto:1.3.2'
    implementation 'org.apache.commons:commons-lang3:3.4'
    implementation 'com.squareup.picasso:picasso:2.5.2'
    implementation 'org.twitter4j:twitter4j-core:3.0.5'
    implementation 'com.netflix.rxjava:rxjava-android:0.18.4'
    implementation 'com.joanzapata.android:android-iconify:1.0.6'
    implementation 'uk.me.lewisdeane.ldialogs:ldialogs:1.0@aar'
    implementation 'com.github.castorflex.smoothprogressbar:library-circular:1.0.1'

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"

    androidTestImplementation fileTree(dir: 'src/androidTest/libs', include: '*.jar')
    androidTestImplementation 'com.squareup:fest-android:1.0.7'
}
